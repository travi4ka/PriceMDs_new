plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'qa.guru'
version '1.0-SNAPSHOT'

allure {
    report {
        version.set("2.20.1")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.20.1") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.9.1',
            'com.codeborne:selenide:6.11.0',
            'com.github.javafaker:javafaker:1.0.2',
            'io.rest-assured:rest-assured:5.3.0',
            'io.qameta.allure:allure-selenide:2.20.1',
            'org.slf4j:slf4j-simple:2.0.6')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
}

task stage_regress(type: Test) {
    useJUnitPlatform {
    }
}

task stage_smoke(type: Test) {
    useJUnitPlatform {
        includeTags("Smoke")
        excludeTags("Regression")
    }
}

task prod(type: Test) {
    useJUnitPlatform {
        includeTags("Prod")
    }
}

test {
    useJUnitPlatform()
}
sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9
